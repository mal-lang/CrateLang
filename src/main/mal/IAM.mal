/*
 * Copyright 2019-2025 tyrLang contributors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

category IAM {

    asset Identity
      user info: "An identity models an IAM identity that should then be associated with privileges on other instances."
      developer info: "An identity can be visualised as a group of assumable roles that can be associated with many credentials."
    {
      # notPresent [Disabled]
        user info: "This defense is used to reason about the attack vectors introduced by this asset. It should be used to model the possibility that the IAM object does not actually exist."
        developer info: "Enabling this defense should make the asset behave as if it did not exist, as such all of its impactful attack steps should be disabled by it."
        ->  assume

      | attemptAssume @hidden
        developer info: "Intermediate attack step to allow for defenses."
        ->  assume

      & assume
        user info: "After authentication or compromise of an account/identity, assume its privileges."
        developer info: "This is both legitimate and illegitimate access! Also assume all the privileges of the parent identities (on the above level/inherited by this identity) because those represent the group of (inherited) roles."
        ->  execPrivApps.authenticate,
            readPrivData.authorizedReadFromIAM,
            writePrivData.authorizedWriteFromIAM,
            deletePrivData.authorizedDeleteFromIAM,
            managedIAMs.attemptAssume,
            parentId.attemptAssume

    }

    asset Credentials
      user info: "Credentials can be used to get access to an Identity, but they can also be used as an encryption/signing key for Data."
      modeler info: "Credentials represent a variety of access control mechanism(e.g. username and password pair, keycards, biometric data)."
    {
      | attemptUse @hidden
        developer info: "In order to use these credentials the attacker may require additional factors."
        ->  use

      & use {C}
        user info: "The attacker is using the credentials to perform a legitimate authentication."
        ->  identities.attemptAssume,
            encryptedData.accessDecryptedData,
            credentials.use // these credentials may serve as additional required factors for other credentials in a multi-factor authentication

      | attemptGuessCredentials
        ->  guessCredentials,
            credentials*.attemptGuessCredentials

      & guessCredentials @hidden [HardAndUncertain]
        developer info: "The attacker can try to just guess a set of credentials. The likelihood of succeeding is dependent on how strong the credentials are."
        modeler info: "The guessability of the Credentials is influenced by the notGuessable defense on this asset and the securityAwareness defense on the User associated with the Identity that these Credentials belong to. If either the User or Identity associations are missing the assumption is made that the crentials are guessable and only the notGuessable defense would play a role in restricting this attack step."
        ->  attemptUse
    }
}

associations {
  Credentials     [encryptCreds]      0..1 <-- EncryptionCredentials --> *    [encryptedData]          Data
      user info: "Encrypted data can be associated with the relevant encryption credentials."
  Credentials     [credentials]          * <-- ConditionalAuthentication --> * [requiredFactors]       Credentials
      user info: "Credentials can be associated with other Credentials to depict conditional authentication procedures, such as multi-factor authentication."
  // ### Access Control happens below
  Identity        [identities]           * <-- IdentityCredentials   --> *    [credentials]            Credentials
  Identity        [parentId]             * <-- CanAssume             --> *    [childId]                Identity
      user info: "Starting from a parent Identity, the child Identities can be assumed due to inheritance."
  // Then, Access Control on application level
  Identity        [executionPrivIAMs]    * <-- ExecutionPrivilegeAccess       --> * [execPrivApps]     Application
      user info: "Every application executes on a system with privileges of a specified identity on the system. If the application is compromised then the privileges should be compromised."
  // Finally, Access control on data
  Identity        [readingIAMs]          * <-- ReadPrivileges        --> *    [readPrivData]           Data
  Identity        [writingIAMs]          * <-- WritePrivileges       --> *    [writePrivData]          Data
  Identity        [deletingIAMs]         * <-- DeletePrivileges      --> *    [deletePrivData]         Data
  // Self-referential associations for the Identity asset
  Identity        [managers]             * <-- AccountManagement     --> *    [managedIAMs]            Identity
      user info: "Identities, Groups, and Privileges may have account management roles for other Identities, Groups, and Privileges."
}
