/*
 * Copyright 2019-2025 tyrLang contributors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

category Networking {

    asset Network
      user info: "A network (zone) is a set of network accessible applications and data."
      developer info: "The network asset tries to cover all the levels of the OSI stack in a very abstract and compact way (i.e. it tries to represent all the OSI layers). For example it will try to cover both ARP attacks (that are Level 2) but also DNS/HTTP spoofing attacks (that are Level 7)."
    {
      let outboundAllowedConnections = (netConnections \/ outgoingNetConnections)
      let allNetConnections = (netConnections \/ ingoingNetConnections \/ outgoingNetConnections \/ diodeIngoingNetConnections)
      let allowedApplicationConnectionsApplications = (applications \/ allNetConnections().applications)

      | access
        user info: "Uninspected access provides connect to all reachable applications without any restrictions."
        ->  outboundAllowedConnections().attemptConnectToApplications,
            applications.networkConnect,
            networkForwarding,
            deny

      | networkForwarding @hidden
        developer info: "By using the allowed connections (connection rules), uninspected forwarding from one network to another network or applications can happen."
          ->  outboundAllowedConnections().attemptAccessNetworks,
              interNetConnections.attemptAccessNetworks,
              outboundAllowedConnections().attemptConnectToApplications

      | deny {A}
        user info: "If a DoS is performed it affects, the applications communicating over the network as well as the connected application."
        ->  allNetConnections().attemptDeny,
            allowedApplicationConnectionsApplications().denyFromNetworkingAsset
            
    }

    asset ConnectionRule
      user info: "This asset is used to model firewall rules between Applications and/or Networks. The association selected (bi/uni-directional) only describes the connection initiation."
      developer info: "This asset is mainly used to model the functionality of a network connection through a firewall. If Data is added, then it also models the data in transit."
    {
      let allApplications = (applications \/ inApplications \/ outApplications)
      let receiverApplications = (applications \/ inApplications)

      # restricted [Disabled]
        user info: "The restricted defense can be used to probabilistically model the likelihood of both the protocols required by the attack being enabled or the existence of the ConnectionRule altogether."
        ->  accessNetworks,
            connectToApplications,
            deny

      | attemptAccessNetworks @hidden
        developer info: "Intermediate attack step to allow for defenses and trigger bypasses."
        ->  accessNetworks

      & accessNetworks
        developer info: "Access all networks that are associated with this ConnectionRule, without any restriction due to inspection."
        ->  (networks  \/ inNetworks  \/ diodeInNetworks).access

      | attemptConnectToApplications @hidden
        developer info: "Intermediate attack step to allow for defenses and trigger bypasses."
        ->  connectToApplications

      & connectToApplications
        developer info: "Connect to all the (server) Applications that are associated with this ConnectionRule, without any restriction due to inspection."
        ->  receiverApplications().networkConnect

      | attemptDeny @hidden
        developer info: "Intermediate attack step to allow for defenses and trigger bypasses."
        ->  deny

      & deny {A}
        user info: "If a connection rule is denied it affects the applications communicating over it."
        ->  allApplications().denyFromNetworkingAsset
    }

    asset InternetworkConnectionRule
    {
      | attemptAccessNetworks @hidden
        developer info: "Intermediate attack step to allow for defenses and trigger bypasses."
        ->  accessNetworks

      & accessNetworks
        developer info: "Access all networks that are associated with this ConnectionRule."
        ->  interNetworks.access
    }
    asset InterappConnectionRule
    {
      | attemptConnectToApplications @hidden
        developer info: "Intermediate attack step to allow for defenses and trigger bypasses."
        ->  connectToApplications

      & connectToApplications
        developer info: "Connect to all the Applications that are associated with this ConnectionRule."
        ->  interApps.networkConnect

    }
}

associations {
  // ### Networking associations
  Network          [networks]             * <-- NetworkExposure       --> *    [applications]           Application
      user info: "An application can communicate / be exposed on a network."
      modeler info: "This is DEPRECATED and should only be used by fully aware modelers."
      developer info: "This can equivalently be modeled using a ConnectionRule but this direct approach should be prefered in the case where the application is “located” in the same network as the host on which it is running."
  Application      [applications]         * <-- ApplicationConnection --> *    [appConnections]         ConnectionRule
      user info: "An application can communicate over one or more (mesh type) bidirectional connections."
      modeler info: "ConnectionRules associated with Applications are practically expressing a traffic flow."
  Application      [inApplications]       * <-- InApplicationConnection --> *  [ingoingAppConnections]  ConnectionRule
      user info: "Two or more applications can also be inter-connected via a uni-directional connection. In means that this is the server side application, awaiting incoming connections."
  Application      [outApplications]      * <-- OutApplicationConnection --> * [outgoingAppConnections] ConnectionRule
      user info: "Two or more applications can also be inter-connected via a uni-directional connection. Out means that this is the client side application initiating connections."
  Network          [networks]             * <-- NetworkConnection     --> *    [netConnections]         ConnectionRule
      user info: "Two or more networks can also be inter-connected via a bidirectional connection."
  Network          [inNetworks]           * <-- InNetworkConnection   --> *    [ingoingNetConnections]  ConnectionRule
      user info: "Two or more networks can also be inter-connected via a uni-directional connection. In means that ingoing traffic to the network is allowed."
  Network          [outNetworks]          * <-- OutNetworkConnection  --> *    [outgoingNetConnections] ConnectionRule
      user info: "Two or more networks can also be inter-connected via a uni-directional connection. Out means that outgoing traffic from the network is allowed."
  Network          [diodeInNetworks]      * <-- DiodeInNetworkConnection --> * [diodeIngoingNetConnections] ConnectionRule
      user info: "Two or more networks can also be inter-connected via a uni-directional connection that acts like a diode. That means that no responses can come back after connection initiation."
  Network          [interNetworks]             * <--  InternetworkConnection --> *    [interNetConnections] InternetworkConnectionRule
      user info: "Two or more networks can also be inter-connected via a bidirectional connection."
  Application      [interApps]               * <--  InterappConnection --> *    [interAppConnections] InterappConnectionRule
      user info: "Two or more applications can also be inter-connected via a bidirectional connection."
}
