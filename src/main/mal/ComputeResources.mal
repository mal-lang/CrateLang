/*
 * Copyright 2019-2025 tyrLang contributors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

category ComputeResources {

    asset Application
      user info: "An application models any type of software process or component, from firmware and operating systems to plugins and sessions."
      modeler info: "Applications are some of the most common assets in models due to their versatility and scope."
    {
      let allVulnerabilities = vulnerabilities
      let outgoingApplicationConnections = (appConnections \/ outgoingAppConnections)

      # notPresent [Disabled]
        user info: "This defense is used to reason about the attack vectors introduced by this asset. It should be used to model the possibility that the Application does not actually exist. Typically this is useful to see what attack steps are removed if a particular non-essential component(e.g. a plugin, library, feature, or service)."
        developer info: "This defense allows for both speculation about the presence of a particular application and to suggest that if it is possible removing it may restrict the attacker's actions."
        ->  useVulnerability,
            networkAccess,
            successfulRead,
            successfulModify,
            successfulDeny

      | attemptUseVulnerability @hidden
        developer info: "Intermediate attack step to allow for defenses and trigger bypasses."
        ->  useVulnerability

      & useVulnerability
        user info: "Try to exploit the associated vulnerabilities."
        developer info: "This includes vulnerabilities associated with the SoftwareProduct that this Application is running as well."
        ->  allVulnerabilities().attemptAbuse

      | networkConnect @hidden
        developer info: "Intermediate attack step."
        ->  networkAccess,
            attemptUseVulnerability,
            executionPrivIAMs.credentials.attemptGuessCredentials // Attempt guessing the Credentials of any idenities present on this host

      | accessNetworkAndConnections @hidden
        developer info: "The attacker can access outgoing and bidirectional networks and connections associated with the application."
        ->  networks.access,
            outgoingApplicationConnections().attemptConnectToApplications,
            outgoingApplicationConnections().attemptAccessNetworks,
            interAppConnections.attemptConnectToApplications

      | bypassContainerization [HardAndUncertain]
        user info: "The attacker is able to break out of an application container/sandbox and try to exploit any vulnerability of the host application."
        ->  hostApp.attemptUseVulnerability

      | authenticate
        user info: "The attacker is able to authenticate with the appropriate high-level privileges."
        ->  networkAccess

      & networkAccess @hidden
        developer info: "Intermediate attack steps for modeling the two requirements (reachability and authentication) for full access."
        ->  fullAccess

      | fullAccess @entrypoint {C,I,A}
        user info: "Full access on the Application means the attacker has complete control of the application and can perform any actions within its execution context."
        modeler info: "It is common in models for the attacker to be granted full access on an Application, representing their own tools, that is connected to the Internet to explore possible attack vectors."
        ->  attemptRead,
            attemptModify,
            attemptDeny,
            executionPrivIAMs.attemptAssume,  // Assume also the execution privilege identities of this application to the access to the entire execution context
            accessNetworkAndConnections  // and access the network(s) and connections on/to which the app is connected

      | attemptRead @hidden
        developer info: "Intermediate attack step to allow for defenses."
        ->  successfulRead

      & successfulRead @hidden
        developer info: "Intermediate attack step to model defenses."
        ->  read

      | read {C}
        user info: "The attacker can read some or all of this service's (and executed by this) source code and/or data (both local and sent/received)."
        ->  containedData.attemptRead,
            appExecutedApps.attemptRead, // Read all applications executed by this (host) application
            sentData.attemptRead, // Both sent and received Data can be read
            receivedData.attemptRead

      | attemptModify @hidden
        developer info: "Intermediate attack step to allow for defenses."
        ->  successfulModify

      & successfulModify @hidden
        developer info: "Intermediate attack step to model defenses."
        ->  modify

      | modify {I}
        user info: "The attacker can modify some or all of this service's (and executed by this) source code and/or data."
        ->  fullAccess,
            containedData.attemptWrite,
            appExecutedApps.attemptModify, // Gain full access to all applications executed by this (host) application
            sentData.attemptWrite // Sent Data can be written/modified

      | attemptDeny @hidden
        developer info: "Intermediate attack step to allow for defenses."
        ->  successfulDeny

      & successfulDeny @hidden
        developer info: "Intermediate attack step to model defenses."
        ->  deny


      | deny {A}
        user info: "The attacker can deny some or all functionality and data pertaining to this application/service as well as executed applications."
        ->  containedData.attemptDeny,
            appExecutedApps.attemptDeny,
            sentData.attemptDeny // Sent Data can also be denied

      & denyFromNetworkingAsset @hidden
        developer info: "This is an intermediate attack step to only allow deny on an application when all the connection rules and networks associated with it are denied, because an app can be serving on many different ports."
        ->  attemptDeny

      & denyFromLockout @hidden
        developer info: "This is an intermediate attack step to only trigger deny on an application when all the executing access control roles are locked out."
        ->  attemptDeny

    }
}

associations {
  Application      [hostApp]           0..1 <-- AppExecution          --> *    [appExecutedApps]        Application
      user info: "Sandboxing and containerization are represented through AppExecution. This application hosting can be nested. For example, application 1 runs application 2 which runs application 3. (Where application 1 is an OS, application 2 is a VM, and application 3 is app running in the VM.)"
}
